plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.parcelize)
    alias(libs.plugins.dokka)
    alias(libs.plugins.publish)
}

android {
    namespace = "com.freeletics.mad.navigator.runtime.fragment"
    compileSdkVersion libs.versions.android.compile.get().toInteger()

    defaultConfig {
        minSdkVersion libs.versions.android.min.get().toInteger()
    }

    buildFeatures {
        buildConfig = false
    }

    // still needed for Android projects despite toolchain
    compileOptions {
        sourceCompatibility(JavaVersion.toVersion(libs.versions.java.get()))
        targetCompatibility(JavaVersion.toVersion(libs.versions.java.get()))
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-37652
android.kotlinOptions.freeCompilerArgs += "-Xexplicit-api=strict"

kotlin {
    explicitApi()

    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(libs.versions.java.get().toInteger()))
    }

    sourceSets.all {
        languageSettings {
            optIn("com.freeletics.mad.navigator.internal.InternalNavigatorApi")
        }
    }
}

// workaround for https://issuetracker.google.com/issues/194113162
tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(libs.versions.java.get().toInteger())
    }
}

dependencies {
    api project(":navigator:navigator-runtime")
    api libs.androidx.fragment
    api libs.androidx.navigation.fragment

    implementation libs.coroutines.core
    implementation libs.androidx.activity
    implementation libs.androidx.lifecycle.common
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.viewmodel.savedstate
    implementation libs.androidx.navigation.common
    implementation libs.androidx.navigation.runtime
    implementation libs.kotlin.parcelize
}