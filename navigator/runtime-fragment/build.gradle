apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.plugin.parcelize'
apply plugin: "org.jetbrains.dokka"
apply plugin: 'com.vanniktech.maven.publish'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
    }

    buildFeatures {
        buildConfig = false
    }

    // still needed for Android projects despite toolchain
    compileOptions {
        sourceCompatibility(JavaVersion.VERSION_11)
        targetCompatibility(JavaVersion.VERSION_11)
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-37652
android.kotlinOptions.freeCompilerArgs += "-Xexplicit-api=strict"

kotlin {
    explicitApi()

    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }

    sourceSets.all {
        languageSettings {
            optIn("kotlin.RequiresOptIn")
            optIn("com.freeletics.mad.navigator.internal.InternalNavigatorApi")
        }
    }
}

// workaround for https://issuetracker.google.com/issues/194113162
tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    api project(":navigator:navigator-runtime")
    api "androidx.fragment:fragment:1.5.0"
    api "androidx.navigation:navigation-fragment:2.5.0"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "androidx.activity:activity:1.5.0"
    implementation "androidx.lifecycle:lifecycle-common:2.5.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.5.0"
    implementation "androidx.navigation:navigation-common:2.5.1"
    implementation "androidx.navigation:navigation-runtime:2.5.0"
    implementation 'org.jetbrains.kotlin:kotlin-parcelize-runtime:1.7.0'
}
