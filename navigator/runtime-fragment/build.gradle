apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'com.vanniktech.maven.publish'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
    }

    // still needed for Android projects despite toolchain
    compileOptions {
        sourceCompatibility(JavaVersion.VERSION_11)
        targetCompatibility(JavaVersion.VERSION_11)
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-37652
android.kotlinOptions.freeCompilerArgs += "-Xexplicit-api=strict"

kotlin {
    explicitApi()

    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }

    sourceSets.all {
        languageSettings {
            optIn("kotlin.RequiresOptIn")
        }
    }
}

// workaround for https://issuetracker.google.com/issues/194113162
tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    api project(":navigator:common-fragment")
    api project(":navigator:runtime")
    api "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"
    api "androidx.fragment:fragment:1.4.1"
    api "androidx.navigation:navigation-runtime-ktx:2.4.0"
    api "androidx.navigation:navigation-fragment-ktx:2.4.0"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0"

    testImplementation "junit:junit:4.13.2"
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "app.cash.turbine:turbine:0.7.0"
}
