apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'com.vanniktech.maven.publish'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
    }

    buildFeatures {
        compose = true
        buildConfig = false
    }

    // still needed for Android projects despite toolchain
    compileOptions {
        sourceCompatibility(JavaVersion.VERSION_11)
        targetCompatibility(JavaVersion.VERSION_11)
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.1.1"
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-37652
android.kotlinOptions.freeCompilerArgs += "-Xexplicit-api=strict"

kotlin {
    explicitApi()

    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }

    sourceSets.all {
        languageSettings {
            optIn("kotlin.RequiresOptIn")
            optIn("com.freeletics.mad.navigator.internal.InternalNavigatorApi")
            optIn("com.freeletics.mad.navigator.internal.ObsoleteNavigatorApi")
        }
    }
}

// workaround for https://issuetracker.google.com/issues/194113162
tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    api project(":navigator:navigator-runtime")
    api "androidx.compose.runtime:runtime:1.1.1"
    api "androidx.navigation:navigation-runtime:2.4.1" // for an obsolete api

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.6.0"
    implementation "androidx.activity:activity:1.4.0"
    implementation "androidx.activity:activity-compose:1.4.0"
    implementation "androidx.compose.ui:ui:1.1.1"
    implementation "androidx.compose.ui:ui-graphics:1.1.1"
    implementation "androidx.compose.animation:animation-core:1.1.1"
    implementation "androidx.compose.foundation:foundation-layout:1.1.1"
    implementation "androidx.lifecycle:lifecycle-common:2.4.1"
    implementation "androidx.lifecycle:lifecycle-livedata-core:2.4.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.4.1"
    implementation "androidx.navigation:navigation-common:2.4.1"
    implementation "androidx.navigation:navigation-compose:2.4.1"
    implementation "com.google.accompanist:accompanist-navigation-material:0.23.1"
}
