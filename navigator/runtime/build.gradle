apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'com.vanniktech.maven.publish'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
    }

    buildFeatures {
        buildConfig = false
    }

    // still needed for Android projects despite toolchain
    compileOptions {
        sourceCompatibility(JavaVersion.VERSION_11)
        targetCompatibility(JavaVersion.VERSION_11)
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-37652
android.kotlinOptions.freeCompilerArgs += "-Xexplicit-api=strict"

kotlin {
    explicitApi()

    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }

    sourceSets.all {
        languageSettings {
            optIn("kotlin.RequiresOptIn")
            optIn("com.freeletics.mad.navigator.internal.InternalNavigatorApi")
            optIn("com.freeletics.mad.navigator.internal.ObsoleteNavigatorApi")
        }
    }
}

// workaround for https://issuetracker.google.com/issues/194113162
tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    api project(":navigator:common")
    api "androidx.activity:activity:1.4.0"
    //TODO exclude internal classes from build health check
    api "androidx.navigation:navigation-runtime:2.4.0"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.6.0"

    implementation "androidx.core:core:1.7.0"
    implementation "androidx.navigation:navigation-common:2.4.0"

    testImplementation "junit:junit:4.13.2"
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "app.cash.turbine:turbine-jvm:0.7.0"
}
