buildscript {
    ext.kotlin_version = '1.6.10'

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.2.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.6.10"
        classpath "com.vanniktech:gradle-maven-publish-plugin:0.20.0"
        classpath "com.autonomousapps:dependency-analysis-gradle-plugin:1.6.0"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.42.0"
        classpath "org.jetbrains.kotlinx:binary-compatibility-validator:0.10.0"
    }
}

apply plugin: "com.autonomousapps.dependency-analysis"
apply plugin: "org.jetbrains.kotlinx.binary-compatibility-validator"
apply plugin: "com.github.ben-manes.versions"


allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyAnalysis {
    issues {
        all {
            onAny {
                severity('fail')
            }

            onUnusedDependencies {
                exclude("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
            }
        }

        project(":whetstone:runtime-compose") {
            onUnusedDependencies {
                exclude(":whetstone:runtime")
            }
            onIncorrectConfiguration {
                exclude(":whetstone:runtime")
            }
        }

        project(":whetstone:runtime-fragment") {
            onUnusedDependencies {
                exclude(":whetstone:runtime")
            }
            onIncorrectConfiguration {
                exclude(":whetstone:runtime")
            }
        }

        project(":whetstone:navigation") {
            onUnusedDependencies {
                exclude(":whetstone:runtime")
            }
            onIncorrectConfiguration {
                exclude(":whetstone:runtime")
            }
        }

        project(":whetstone:navigation-compose") {
            onUnusedDependencies {
                exclude(":whetstone:navigation", ":whetstone:runtime-compose")
            }
            onIncorrectConfiguration {
                exclude(":whetstone:navigation", ":whetstone:runtime-compose")
            }
        }

        project(":whetstone:navigation-fragment") {
            onUnusedDependencies {
                exclude(":whetstone:navigation", ":whetstone:runtime-fragment")
            }
            onIncorrectConfiguration {
                exclude(":whetstone:navigation", ":whetstone:runtime-fragment")
            }
        }
    }

    abi {
        exclusions {
            excludeClasses(".*\\.navigator\\.internal\\..*")
        }
    }
}

apiValidation {
    ignoredProjects += ["compiler"]

    nonPublicMarkers += [
        "androidx.annotation.VisibleForTesting",
        "com.freeletics.mad.navigator.internal.InternalNavigatorApi",
        "com.freeletics.mad.navigator.internal.ObsoleteNavigatorApi",
        "com.freeletics.mad.whetstone.internal.InternalWhetstoneApi",
    ]
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }
}
