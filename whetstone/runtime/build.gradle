apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'com.vanniktech.maven.publish'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
    }

    buildFeatures {
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.0.4"
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-37652
android.kotlinOptions.freeCompilerArgs += "-Xexplicit-api=strict"

kotlin {
    explicitApi()

    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }

    sourceSets.all {
        languageSettings {
            optIn("kotlin.RequiresOptIn")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    api "javax.inject:javax.inject:1"
    api "androidx.lifecycle:lifecycle-viewmodel:2.4.0"
    api "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.4.0"
    api "androidx.navigation:navigation-runtime:2.3.5"
    api "com.google.dagger:dagger:2.40"
    api project(":state-machine")
    api project(":navigator")

    implementation "androidx.savedstate:savedstate:1.1.0"
    implementation "androidx.activity:activity:1.4.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.5.2"

    // dependencies to be added by consumers depending on what they use
    compileOnly "androidx.compose.runtime:runtime:1.0.4"
    compileOnly "androidx.compose.ui:ui:1.0.4"
    compileOnly "androidx.lifecycle:lifecycle-viewmodel-compose:2.4.0"
    compileOnly "androidx.fragment:fragment:1.3.6"
    compileOnly "com.gabrielittner.renderer:renderer:0.10.2"
}
