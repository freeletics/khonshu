apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.vanniktech.maven.publish'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
    }

    buildFeatures {
        buildConfig = false
    }

    // still needed for Android projects despite toolchain
    compileOptions {
        sourceCompatibility(JavaVersion.VERSION_11)
        targetCompatibility(JavaVersion.VERSION_11)
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-37652
android.kotlinOptions.freeCompilerArgs += "-Xexplicit-api=strict"

kotlin {
    explicitApi()

    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }

    sourceSets.all {
        languageSettings {
            optIn("kotlin.RequiresOptIn")
            optIn("com.freeletics.mad.whetstone.internal.InternalWhetstoneApi")
        }
    }
}

// workaround for https://issuetracker.google.com/issues/194113162
tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    api project(":whetstone:navigation")
    api project(":whetstone:runtime-fragment")
    api project(":navigator:navigator-runtime")

    implementation project(":whetstone:runtime")
    implementation "androidx.compose.runtime:runtime:1.1.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.5.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.5.0"
    implementation "androidx.fragment:fragment:1.5.0"
    implementation "androidx.navigation:navigation-common:2.5.0"
    implementation "androidx.navigation:navigation-runtime:2.5.0"
    implementation "androidx.navigation:navigation-fragment:2.5.1"
}
